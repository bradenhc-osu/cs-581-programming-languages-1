module HWReview where

import Prelude hiding (Enum(..), sum)

-- | A representation of arithmetic expressions as binary trees where leaves
--   are literal integers and internal nodes are either addition or
--   multiplaction nodes. Note that this data structure is an "abstract
--   syntax tree", which is something we will spend more time talking about
--   later in the course.
data Expr
    = Lit Int         -- ^ Literal integers
    | Add Expr Expr   -- ^ Addition expressions
    | Mul Expr Expr   -- ^ Multiplication expressions
    deriving (Eq,Show)

------------------------------------------------------------------------------------------------------------------------
-- HOMEWORK 1
------------------------------------------------------------------------------------------------------------------------

-- | The expression: 2 + 3 * 4
e1 :: Expr
e1 = Add (Lit 2) (Mul (Lit 3) (Lit 4))


-- | The expression: (7 + 6) * 5
e2 :: Expr
e2 = Mul (Add (Lit 7) (Lit 6)) (Lit 5)


-- | The expresssion: 3 * 2 + 5 * 4
e3 :: Expr
e3 = Add (Mul (Lit 3) (Lit 2)) (Mul (Lit 5) (Lit 4))


-- | The expression: 8 + 7 * 9 + 6
e4 :: Expr
e4 = Add (Add (Lit 8) (Mul (Lit 7) (Lit 9))) (Lit 6)


-- | The leftmost literal in an expression.
--
--   >>> leftLit (Lit 3)
--   3
--
--   >>> leftLit e1
--   2
--   
--   >>> leftLit e2
--   7
--
leftLit :: Expr -> Int
leftLit (Lit l)   = l
leftLit (Add l r) = leftLit l
leftLit (Mul l r) = leftLit l


-- | The rightmost literal in an expression.
--
--   >>> rightLit (Lit 3)
--   3
--
--   >>> rightLit e3
--   4
--   
--   >>> rightLit e4
--   6
--
rightLit :: Expr -> Int
rightLit (Lit l)   = l
rightLit (Add l r) = rightLit r
rightLit (Mul l r) = rightLit r


-- | Get the maximum literal value in an expression.
--
--   >>> maxLit (Lit 3)
--   3
--
--   >>> maxLit e1
--   4
--
--   >>> maxLit e2
--   7
--
--   >>> maxLit e3
--   5
--
--   >>> maxLit e4
--   9
--
maxLit :: Expr -> Int
maxLit (Lit x)   = x
maxLit e = case e of
    (Add l r) -> cmp l r
    (Mul l r) -> cmp l r
    where
        cmp l r = let x = maxLit l
                      y = maxLit r
                    in
                        if x > y then x else y


-- | The integer result of evaluating an expression.
--
--   >>> eval (Lit 3)
--   3
--
--   >>> eval e1
--   14
--
--   >>> eval e2
--   65
--
--   >>> eval e3
--   26
--
--   >>> eval e4
--   77
--
eval :: Expr -> Int
eval (Lit x)   = x
eval (Add l r) = (eval l) + (eval r)
eval (Mul l r) = (eval l) * (eval r)

------------------------------------------------------------------------------------------------------------------------
-- HOMEWORK 2
------------------------------------------------------------------------------------------------------------------------

-- | A string rendering of the expression in Reverse Polish Notation (RPN).
--
--   >>> toRPN (Lit 3)
--   "3"
--
--   >>> toRPN e1
--   "2 3 4 * +"
--
--   >>> toRPN e2
--   "7 6 + 5 *"
--
--   >>> toRPN e3
--   "3 2 * 5 4 * +"
--
--   >>> elem (toRPN e4) ["8 7 9 * + 6 +", "8 7 9 * 6 + +"]
--   True
--   
toRPN :: Expr -> String
toRPN (Lit x)   = show x
toRPN (Add l r) = (toRPN l) ++ " " ++ (toRPN r) ++ " +"
toRPN (Mul l r) = (toRPN l) ++ " " ++ (toRPN r) ++ " *"


-- | Convert a string rendering of an expression in RPN into an expression
--   represented as an abstract syntax tree. You can assume that your function
--   will only be given valid strings, i.e. it need not fail gracefully if it
--   encounters an error.
--
--   >>> fromRPN "3"
--   Lit 3
--
--   >>> fromRPN "2 3 +"
--   Add (Lit 2) (Lit 3)
--
--   >>> fromRPN "2 3 4 + +"
--   Add (Lit 2) (Add (Lit 3) (Lit 4))
--
--   >>> all (\e -> e == fromRPN (toRPN e)) [e1,e2,e3,e4]
--   True
--
fromRPN :: String -> Expr
fromRPN s = build (words s) []
    where
        build []       (e:es)   = e
        build ("+":ss) (r:l:es) = build ss ((Add l r) : es)
        build ("*":ss) (r:l:es) = build ss ((Mul l r) : es)
        build (x:ss)   es       = build ss ((Lit (read x)) : es)

------------------------------------------------------------------------------------------------------------------------
-- HOMEWORK 3
------------------------------------------------------------------------------------------------------------------------

-- | Convert a regular list into a run-length list.
--
--   >>> compress [1,1,1,2,3,3,3,1,2,2,2,2]
--   [(3,1),(1,2),(3,3),(1,1),(4,2)]
-- 
--   >>> compress "Mississippi"
--   [(1,'M'),(1,'i'),(2,'s'),(1,'i'),(2,'s'),(1,'i'),(2,'p'),(1,'i')]
--
compress :: Eq a => [a] -> [(Int,a)]
compress = map (\x -> (length x, head x)) . grp
    where
        grp :: Eq a => [a] -> [[a]]
        grp []     = []
        grp x      = let (g, rest) = gather x in g : (grp rest)

        gather :: Eq a => [a] -> ([a],[a])
        gather []         = ([],[])
        gather [x]        = ([x], [])
        gather (x:y:rest) = if x == y then let (r, s) = gather (y : rest) in (x : r, s) else ([x], y : rest)

-- | Convert a run-length list back into a regular list.
--
--   >>> decompress [(5,'a'),(3,'b'),(4,'c'),(1,'a'),(2,'b')]
--   "aaaaabbbccccabb"
--  
decompress :: [(Int,a)] -> [a]
decompress []           = []
decompress ((n,e):rest) = (replicate n e) ++ decompress rest

------------------------------------------------------------------------------------------------------------------------
-- HOMEWORK 4
------------------------------------------------------------------------------------------------------------------------

--
-- * Natural numbers
--

-- | The natural numbers.
data Nat
    = Zero
    | Succ Nat
    deriving (Eq,Show)

-- | The number 1.
one :: Nat
one = Succ Zero

-- | The number 2.
two :: Nat
two = Succ one

-- | The number 3.
three :: Nat
three = Succ two

-- | The number 4.
four :: Nat
four = Succ three


-- | The predecessor of a natural number.
--   
--   >>> pred Zero
--   Zero
--   
--   >>> pred three
--   Succ (Succ Zero)
--   
pred :: Nat -> Nat
pred Zero     = Zero
pred (Succ n) = n


-- | True if the given value is zero.
--
--   >>> isZero Zero
--   True
--
--   >>> isZero two
--   False
--
isZero :: Nat -> Bool
isZero Zero = True
isZero _    = False


-- | Convert a natural number to an integer. NOTE: We use this function in
--   tests, but you should not use it in your other definitions!
--
--   >>> toInt Zero
--   0
--
--   >>> toInt three
--   3
--
toInt :: Nat -> Int
toInt Zero = 0
toInt (Succ n) = 1 + (toInt n)


-- | Add two natural numbers.
--
--   >>> add one two
--   Succ (Succ (Succ Zero))
--
--   >>> add Zero one == one
--   True
--
--   >>> add two two == four
--   True
--
--   >>> add two three == add three two
--   True
--   
add :: Nat -> Nat -> Nat
add n Zero = n
add n (Succ m) = add (Succ n) m


-- | Subtract the second natural number from the first. Return zero
--   if the second number is bigger.
--
--   >>> sub two one
--   Succ Zero
--   
--   >>> sub three one
--   Succ (Succ Zero)
--
--   >>> sub one one
--   Zero
--
--   >>> sub one three
--   Zero
--
sub :: Nat -> Nat -> Nat
sub Zero _            = Zero
sub n Zero            = n
sub (Succ n) (Succ m) = sub n m


-- | Is the left value greater than the right?
--
--   >>> gt one two
--   False
--
--   >>> gt two one
--   True
--
--   >>> gt two two
--   False
--
gt :: Nat -> Nat -> Bool
gt n m = not (isZero (sub n m))


-- | Multiply two natural numbers.
--
--   >>> mult two Zero
--   Zero
--
--   >>> mult Zero three
--   Zero
--
--   >>> toInt (mult two three)
--   6
--
--   >>> toInt (mult three three)
--   9
--
mult :: Nat -> Nat -> Nat
mult Zero _ = Zero
mult _ Zero = Zero
mult n (Succ Zero) = n
mult n (Succ m) = add n (mult n m)


-- | Compute the sum of a list of natural numbers.
--
--   >>> sum []
--   Zero
--   
--   >>> sum [one,Zero,two]
--   Succ (Succ (Succ Zero))
--
--   >>> toInt (sum [one,two,three])
--   6
--
sum :: [Nat] -> Nat
sum ns = foldr (\n b -> add n b) Zero ns


-- | An infinite list of all of the *odd* natural numbers, in order.
--
--   >>> map toInt (take 5 odds)
--   [1,3,5,7,9]
--
--   >>> toInt (sum (take 100 odds))
--   10000
--
odds :: [Nat]
odds = one : map (add two) odds